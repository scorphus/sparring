defmodule Todo.Cache do
  use GenServer

  def start do
    IO.puts("Starting to-do cache")
    GenServer.start(__MODULE__, nil)
  end

  def server_process(cache_pid, todo_list_name) do
    GenServer.call(cache_pid, {:server_process, todo_list_name})
  end

  @impl GenServer
  def init(_) do
    Todo.Database.start()
    {:ok, %{}}
  end

  @impl GenServer
  def handle_call({:server_process, todo_list_name}, _from, todo_servers) do
    case Map.fetch(todo_servers, todo_list_name) do
      {:ok, todo_server} ->
        {:reply, todo_server, todo_servers}

      :error ->
        {:ok, new_server} = Todo.Server.start(todo_list_name)

        {
          :reply,
          new_server,
          Map.put(todo_servers, todo_list_name, new_server)
        }
    end
  end
end

# Usage:
#
# {:ok, cache} = Todo.Cache.start()
# bobs_list = Todo.Cache.server_process(cache, "bobs_list")
# Todo.Server.entries(bobs_list, ~D[2018-12-19])
